import org.apache.tools.ant.filters.ReplaceTokens

plugins {
    id 'java'
    id 'application'
    id 'com.gorylenko.gradle-git-properties' version '2.2.4'
    id 'com.github.johnrengelman.shadow' version '6.1.0'
    id 'com.github.ben-manes.versions' version '0.38.0'
}


group 'space.npstr.icu'
version '0.1.0-SNAPSHOT'

mainClassName = 'space.npstr.icu.Main'

sourceCompatibility = targetCompatibility = JavaVersion.VERSION_11

repositories {
    jcenter()                               // JDA, maybe others
    mavenCentral()                          // everything else
    maven { url 'https://dl.bintray.com/napster/SqlSauce' }
    maven { url 'https://jitpack.io' }      // for getting builds from github
}


dependencies {
    //@formatter:off
    ext {
        jdaVersion              = '[4.1.2_191, 4.3['
        logbackVersion          = '[1.2.3, 2.0['
        sentryVersion           = '[4.3.0, 5.0['
        okhttpVersion           = '[3.14.1, 4.0['
        yamlVersion             = '[1.24, 2.0['
        sqlsauceVersion         = '[0.4.3, 0.5['
        hibernateVersion        = '[5.4.2.Final, 6.0['
        annotationsVersion      = '[0.0.1, 0.1['
        jaxbApiVersion          = '[2.3.1, 3.0['
        caffeineVersion         = '[3.0.1, 4.0['
        dsProxyVersion          = '[1.5.1, 2.0['
    }

    implementation "net.dv8tion:JDA:$jdaVersion"
    implementation "ch.qos.logback:logback-classic:$logbackVersion"
    implementation "io.sentry:sentry-logback:$sentryVersion"
    implementation "com.squareup.okhttp3:okhttp:$okhttpVersion"
    implementation "org.yaml:snakeyaml:$yamlVersion"
    implementation "space.npstr.SqlSauce:sqlsauce-core:$sqlsauceVersion"
    implementation "space.npstr.SqlSauce:discord-entities:$sqlsauceVersion"
    implementation "org.hibernate:hibernate-jcache:$hibernateVersion"
    implementation "com.github.ben-manes.caffeine:jcache:$caffeineVersion"
    implementation "space.npstr:annotations:$annotationsVersion"
    implementation "javax.xml.bind:jaxb-api:$jaxbApiVersion"
    implementation "net.ttddyy:datasource-proxy:$dsProxyVersion"

    //@formatter:on
}

compileJava.dependsOn 'clean'
compileJava.options.encoding = 'UTF-8'
compileJava.options.compilerArgs << "-Xlint:unchecked" << "-Xlint:deprecation"

shadowJar {
    archiveFileName.set("icu.jar")
}

processResources {
    //inject values into app.properties
    filesMatching("**/app.properties") {
        filter ReplaceTokens, tokens: [
                "project.version"   : project.version,
                "project.groupId"   : project.group,
                "project.artifactId": project.name,
                "env.BUILD_NUMBER"  : (System.getenv('CI') ? System.getenv('BUILD_NUMBER') : 'DEV'),
                "env.BUILD_TIME"    : System.currentTimeMillis() + ''
        ]
    }
}

dependencyLocking {
    lockAllConfigurations()
}

// ./gradlew resolveAndLockAll --write-locks
task resolveAndLockAll {
    doFirst {
        assert gradle.startParameter.writeDependencyLocks
    }
    doLast {
        configurations.all {
            resolutionStrategy {
                componentSelection properReleasesOnly()
            }
        }
        configurations
                .findAll { it.canBeResolved }
                .each { it.resolve() }
    }
}

dependencyUpdates.resolutionStrategy {
    componentSelection properReleasesOnly()
}

static def properReleasesOnly() {
    return { rules ->
        rules.all { ComponentSelection selection ->
            boolean rejected = ['alpha', 'beta', 'rc', 'preview', 'm1', 'm2', 'm3'].any {
                q -> selection.candidate.version.toLowerCase().contains(q)
            }
            if (rejected) {
                selection.reject('Release candidate')
            }
        }
    }
}
