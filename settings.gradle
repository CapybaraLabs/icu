pluginManagement {
	repositories {
		gradlePluginPortal()
		mavenCentral()
	}
}

rootProject.name = 'icu'

ext {
	//@formatter:off

	//plugin versions
	gradleGitVersion        = '[2.4.0, 3.0['
	shadowVersion           = '7.+'
	versionsVersion         = '0.+'

	jdaVersion              = '[4.4.0_351, 4.5['
	logbackVersion          = '[1.2.3, 2.0['
	sentryVersion           = '[6.0.0, 7.0['
	okhttpVersion           = '[3.14.1, 4.0['
	yamlVersion             = '[1.24, 2.0['
	sqlsauceVersion         = '[0.4.3, 0.5['
	hibernateVersion        = '5.4.27.Final'
	annotationsVersion      = '[0.0.1, 0.1['
	jaxbApiVersion          = '[2.3.1, 3.0['
	caffeineVersion         = '[3.0.1, 4.0['
	dsProxyVersion          = '[1.5.1, 2.0['

	//@formatter:on
}

dependencyResolutionManagement {

	versionCatalogs {
		libs {
			version("sqlsauce", "$sqlsauceVersion")

			plugin("gitprops", "com.gorylenko.gradle-git-properties").version("$gradleGitVersion")
			plugin("shadow", "com.github.johnrengelman.shadow").version("$shadowVersion")
			plugin("versions", "com.github.ben-manes.versions").version("$versionsVersion")

			library("jda", "net.dv8tion:JDA:$jdaVersion")
			library("logback", "ch.qos.logback:logback-classic:$logbackVersion")
			library("sentry", "io.sentry:sentry-logback:$sentryVersion")
			library("okhttp", "com.squareup.okhttp3:okhttp:$okhttpVersion")
			library("yaml", "org.yaml:snakeyaml:$yamlVersion")
			library("sqlsauce-core", "space.npstr.SqlSauce", "sqlsauce-core").versionRef("sqlsauce")
			library("sqlsauce-discord", "space.npstr.SqlSauce", "discord-entities").versionRef("sqlsauce")
			library("hibernate-jcache", "org.hibernate:hibernate-jcache:$hibernateVersion")
			library("caffeine-jcache", "com.github.ben-manes.caffeine:jcache:$caffeineVersion")
			library("annotations", "space.npstr:annotations:$annotationsVersion")
			library("jaxb", "javax.xml.bind:jaxb-api:$jaxbApiVersion")
			library("dsproxy", "net.ttddyy:datasource-proxy:$dsProxyVersion")
		}
	}
}
