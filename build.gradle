import org.apache.tools.ant.filters.ReplaceTokens
import org.gradle.api.tasks.wrapper.Wrapper.DistributionType

plugins {
    id 'java'
    id 'application'
    id 'idea'
    id 'com.gorylenko.gradle-git-properties' version '1.4.20'
    id 'com.github.johnrengelman.shadow' version '2.0.2'
}


group 'space.npstr.icu'
version '0.0.2-SNAPSHOT'
ext {
    moduleName = 'icu'
}

mainClassName = 'space.npstr.icu.Main'

sourceCompatibility = 9
targetCompatibility = 9

repositories {
    jcenter()                               // JDA, maybe others
    mavenCentral()                          // everything else
    maven { url 'https://dl.bintray.com/napster/SqlSauce' }
    mavenLocal()                            // local maven repo, mostly for testing stuff
    maven { url 'https://jitpack.io' }      // for getting builds from github
}


dependencies {
    //@formatter:off
    ext.jdaVersion              = '3.5.0_338'
    ext.logbackVersion          = '1.2.3'
    ext.sentryVersion           = '1.6.8'
    ext.okhttpVersion           = '3.9.1'
    ext.yamlVersion             = '1.19'
    ext.sqlsauceVersion         = '0.0.10'
    ext.hibernateVersion        = '5.2.12.Final'
    ext.annotationsVersion      = '0.0.1'
    ext.jaxbApiVersion          = '2.3.0'
    ext.caffeineVersion         = '2.6.1'
    ext.dsProxyVersion          = '1.4.6'

    compile group: 'net.dv8tion',    name: 'JDA',                 version: ext.jdaVersion        // discord API
    compile group: 'ch.qos.logback', name: 'logback-classic',     version: ext.logbackVersion    // logging framework
    compile group: 'io.sentry',      name: 'sentry-logback',      version: ext.sentryVersion     // error aggregation
    compile group: 'com.squareup.okhttp3', name: 'okhttp',        version: ext.okhttpVersion     // http client
    compile group: 'org.yaml',       name: 'snakeyaml',           version: ext.yamlVersion       // parse yaml files
    compile (group: 'space.npstr.SqlSauce', name: 'sqlsauce-core', version: ext.sqlsauceVersion) {
        exclude (group: 'org.hibernate', module: 'hibernate-core')
    }   // database lib
    compile group: 'org.hibernate',  name: 'hibernate-core',      version: ext.hibernateVersion  // hibernate
    compile group: 'space.npstr.SqlSauce',name:'discord-entities',version: ext.sqlsauceVersion   // additional db module
    compile group: 'org.hibernate',  name: 'hibernate-ehcache',   version: ext.hibernateVersion  // entity cacheing
    compile group: 'space.npstr',    name: 'annotations',         version: ext.annotationsVersion// advanced annotations
    compile group: 'javax.xml.bind', name: 'jaxb-api',            version: ext.jaxbApiVersion    // required by hibernate for java 9
    compile group: 'com.github.ben-manes.caffeine', name:'caffeine', version:ext.caffeineVersion // caches
    compile group: 'net.ttddyy',     name: 'datasource-proxy',    version: ext.dsProxyVersion    // log proxy for db queries

    //@formatter:on
}

task wrapper(type: Wrapper) {
    gradleVersion = '4.4.1'
    distributionType = DistributionType.ALL
}

compileJava.dependsOn 'clean'
compileJava.options.encoding = 'UTF-8'
compileJava.options.compilerArgs << "-Xlint:unchecked" << "-Xlint:deprecation"

shadowJar {
    archiveName = "icu.jar"
}

processResources {
    //inject values into app.properties
    filesMatching("**/app.properties") {
        filter ReplaceTokens, tokens: [
                "project.version"   : project.version,
                "project.groupId"   : project.group,
                "project.artifactId": project.ext.moduleName,
                "env.BUILD_NUMBER"  : (System.getenv('CI') ? System.getenv('BUILD_NUMBER') : 'DEV'),
                "env.BUILD_TIME"    : System.currentTimeMillis() + ''
        ]
    }
}
